apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7
targetCompatibility = 1.7
group = 'net.dongliu'
version = '2.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.+'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'jvcdiff'
                packaging 'jar'
                description 'Vcdiff decode / encode java implement'
                url 'https://github.com/xiaxiaocao/jvcdiff'

                scm {
                    url 'https://github.com/xiaxiaocao/jvcdiff'
                    connection 'scm:git:git@github.com:xiaxiaocao/jvcdiff.git'
                    developerConnection 'scm:git:git@github.com:xiaxiaocao/jvcdiff.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'xiaxiaocao'
                        name 'Dong Liu'
                    }
                }
            }
        }
    }
}
